//////////////////////////////////////////////////////////////////////////////////////////
//	Modified	: 03.02.2023 - 14.12.2023
//	Author		: Mihan323
//	From Stalker DWM
//  *Some modified by Deathman*
//////////////////////////////////////////////////////////////////////////////////////////
#include "common.h"
#include "gbuffer.h"
//////////////////////////////////////////////////////////////////////////////////////////
// Options
#define USE_ATTENUATION
#define HBAO_ATTENUATION_DISTANCE 0.5h
#define HBAO_NORMAL float(0.0050)
#define HBAO_POWER  float(2.2)
#define	HBAO_RADIUS float(0.4)
//////////////////////////////////////////////////////////////////////////////////////////
// Presets
#if AO_QUALITY == LOW_QUALITY
#define HBAO_SAMPLES 4
#define HBAO_RENDER_DISTANCE 20.0h
#elif AO_QUALITY == MIDDLE_QUALITY
#define HBAO_SAMPLES 4
#define HBAO_RENDER_DISTANCE 30.0h
#elif AO_QUALITY == HIGHT_QUALITY
#define HBAO_SAMPLES 6
#define HBAO_RENDER_DISTANCE 40.0h
#elif AO_QUALITY == ULTRA_QUALITY
#define HBAO_SAMPLES 8
#define HBAO_RENDER_DISTANCE 50.0h
#elif AO_QUALITY == UNDEFINED_QUALITY
#define HBAO_SAMPLES 1
#define HBAO_RENDER_DISTANCE 25.0h
#endif
//////////////////////////////////////////////////////////////////////////////////////////
float3 get_blue_noise (float2 UV)
{
    return tex2Dlod0 (s_blue_noise, UV * screen_res.xy / 256.0h);
}
//////////////////////////////////////////////////////////////////////////////////////////
float3 calc_sphere (float3 hash)
{
    float cos_theta = 1.0h - hash.x;
    float sin_theta = sqrt (1.0h - cos_theta * cos_theta);

    float phi = 2.0h * PI * hash.y;

	float3 sphere;
    sphere.x = sin_theta * cosapprox (phi);
    sphere.y = sin_theta * sinapprox (phi);
    sphere.z = cos_theta;
    sphere *= hash.z;

    return sphere;
}
//////////////////////////////////////////////////////////////////////////////////////////
float3 SampleHemisphereDirection (float3 Normal, float3 hash)
{
    float3 sphere = calc_sphere (hash);

	float3 Up = float3 (1.0h, 0.0h, 0.0h);
    
    if (abs (Normal.z) < 0.999h)
        Up = float3 (0.0h, 0.0h, 1.0h);
    
	float3 Tangent = normalize (cross (Up, Normal));
	float3 Binormal = cross (Normal, Tangent);

    return float3 ((Tangent * sphere.x) + (Binormal * sphere.y) + (Normal * sphere.z));
}
//////////////////////////////////////////////////////////////////////////////////////////
float4 main (float2 TexCoords: TEXCOORD0): COLOR
{
    float3 P = GetPosition (TexCoords);
    
    if (P.z > HBAO_RENDER_DISTANCE || P.z < HBAO_ATTENUATION_DISTANCE)
    {
        return float4 (1.0h, 1.0h, 1.0h, 1.0h);
    }
    else
    {
	    float3 N = tex2D (s_gbuffer_normal, TexCoords);
		
	    float3 position = P + N * HBAO_NORMAL * P.z;

        float occ = 0.0h;
        
        for (int i = 0; i < HBAO_SAMPLES; i++)
        {
            float3 Noise = get_blue_noise (TexCoords + poissonDisk_16[i] * 2.0h);
            
            float3 hemi = SampleHemisphereDirection (N, Noise);
            
		    float3 position_tap = position + hemi * HBAO_RADIUS;

		    float4 sample_proj = calc_proj (position_tap);
		    float2 sample_tap = projection_to_screen (sample_proj);

            float depth_hit = GetPosition (sample_tap).z;

            float dist = position_tap.z - depth_hit;
		
            if (dist >= 0.0h && dist <= 1.0h)
                occ += 1.0h - dist * dist;
        }

        occ = saturate (1.0h - occ / HBAO_SAMPLES);

        // Create smooth render distance border
        float FarFadingFactor = smoothstep (HBAO_RENDER_DISTANCE * 0.75h, HBAO_RENDER_DISTANCE, length (P));
        occ = lerp (1.0h, occ, 1.0h - FarFadingFactor);
        
#ifdef USE_ATTENUATION
        float attenuation = smoothstep (HBAO_ATTENUATION_DISTANCE, HBAO_ATTENUATION_DISTANCE * 1.75h, length (P));
	    occ = lerp(1.0h, occ, attenuation);
#endif

        return pow (occ, HBAO_POWER).xxxx;
    }
}
//////////////////////////////////////////////////////////////////////////////////////////
