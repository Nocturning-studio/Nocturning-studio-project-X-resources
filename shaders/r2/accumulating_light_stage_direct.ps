///////////////////////////////////////////////////////////////////////////////////
// Created: 20.11.2023
// Author: Deathman
// Nocturning studio for NS Project X
///////////////////////////////////////////////////////////////////////////////////
#include "common.xrh"
#include "gbuffer.xrh"
#include "BRDF.xrh"
#include "accumulation_helper.xrh"
#include "shadow_filtering.xrh"
#include "screen_space_shadows.xrh"
///////////////////////////////////////////////////////////////////////////////////
// Constants
uniform float sun_far;
///////////////////////////////////////////////////////////////////////////////////
struct Interpolators
{
    float4 HomogeniousPosition: POSITION;
    float4 TexCoords: TEXCOORD0;
};
///////////////////////////////////////////////////////////////////////////////////
Accumulator main (Interpolators Input)
{
    GBuffer GBuffer = UnpackGBuffer (Input.TexCoords.xy/Input.TexCoords.w);

    LightComponents Light = Calculate_Lighting_Model (GBuffer.Roughness, GBuffer.Metallness, GBuffer.Albedo, GBuffer.Position, GBuffer.Normal, GBuffer.BakedAO, Ldynamic_dir.xyz);
    
    float4 Position = float4 (GBuffer.Position + GBuffer.Normal * 0.01575f, 1.0f);
    float4 ShadowMapTexCoords = mul (m_shadow, Position);
    float ShadowMap = get_shadow_map (ShadowMapTexCoords);
    
    // Fading with distance
#ifdef USE_SMOOTH_FADING
    float ShadowMapFadingFactor = smoothstep(sun_far * 0.85f, sun_far, length (GBuffer.Position));
    ShadowMap = lerp (ShadowMap, 1.0f, ShadowMapFadingFactor);
#endif    

    float NdotL = abs(dot(normalize(GBuffer.Normal), Ldynamic_dir.rgb));
    if((NdotL * ShadowMap > 0.1f) && (length(GBuffer.Position) < 1.5f))
        ShadowMap *= TraceShadow(Ldynamic_dir, Position);

    float3 SunColor = Ldynamic_color.rgb;// * 2.0f;

    Light.Diffuse *= SunColor * ShadowMap.xxx;
    Light.Diffuse += Light.Subsurface * SunColor * GBuffer.Subsurface * ShadowMap.xxx;
    Light.Specular *= SunColor * ShadowMap.xxx;

    Accumulator Output = PackLightingAccumulator (Light.Diffuse, Light.Specular);
    return Output;
}
///////////////////////////////////////////////////////////////////////////////////
