////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Author: Deadiablo, Deathman
// Original code: https://www.xrhadertoy.com/view/WscyRl
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
#include "material_components.xrh"
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
#define AO_SAMPLES 8.0
#define AO_ATTENUATION 5.0
#define AO_RADIUS 0.1
#define AO_POWER 4.4
#define AO_RENDER_DISTANCE 25.0f
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
float AOfromHeight(float2 UV, float WorldSpaceDepth
#ifdef USE_FOR_TERRAIN_MATERIAL
    , float4 BlendMask
#endif
)
{
    float AO = 1.0f;

    if (WorldSpaceDepth < AO_RENDER_DISTANCE)
    {
#ifdef USE_FOR_TERRAIN_MATERIAL
        float height = GetTerrainDetailHeight(BlendMask, UV);
#else
        float height = GetHeight(UV);
#endif

        float result = 0.0;
        
        float c, s;
        sincos(PI, c, s);
        float2x2 rot = float2x2(c, -s, s, c);
        float2 dir;
        sincos(5.0f * PI, dir.y, dir.x);
        
        [unroll]
        for (float f = 0.0; f < 1.0; f += 1.0 / AO_SAMPLES)
        {
            dir = mul(dir, rot);
            float noiseDirection = random(float2(f * 16524.56, f * 23532.43));
            float2 noiseOffset = noiseDirection * dir * AO_RADIUS;
            noiseOffset = noiseOffset * 2.0 - 1.0;

#ifdef USE_FOR_TERRAIN_MATERIAL
            result += max(GetTerrainDetailHeight(BlendMask, UV + noiseOffset) - height, 0.0f);
#else
            result += max(GetHeight(UV + noiseOffset) - height, 0.0f);
#endif
        }

        result /= AO_SAMPLES;
        result = pow(1.0 - result, AO_POWER);

        // Create smooth render distance border
        float FarFadingFactor = smoothstep(AO_RENDER_DISTANCE * 0.75f, AO_RENDER_DISTANCE, WorldSpaceDepth);
        result = lerp(result, 1.0f, FarFadingFactor);

        AO = result;
    }

    return AO;
}
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////